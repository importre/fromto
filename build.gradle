buildscript {
    ext.kotlin_version = '1.0.0-beta-3595'
    ext.dokka_version = '0.9.3'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.4'
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'signing'

def FromToGroupId = 'io.github.importre'
def FromToVersion = '0.3.1'
def FromToArtifact = 'fromto'
def FromToUrl = 'https://github.com/importre/fromto'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: 'dokka') {
    classifier = 'javadoc'
    from 'build/dokka'
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        FromToPublish(MavenPublication) {
            from components.java
            groupId FromToGroupId
            artifactId FromToArtifact
            version FromToVersion

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty("user") ? project.user : ""
    key = project.hasProperty("key") ? project.key : ""
    publications = ['FromToPublish']
    publish = true
    //dryRun = true

    pkg {
        repo = 'maven'
        name = FromToArtifact
        licenses = ['Apache-2.0']
        vcsUrl = "${FromToUrl}.git"
        publicDownloadNumbers = true

        version {
            name = FromToVersion
            vcsTag = FromToVersion
            gpg {
                sign = true
                passphrase = project['signing.password']
            }
        }
    }
}

group FromToGroupId
version FromToVersion

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'io.reactivex:rxjava:1.1.0'

    testCompile 'junit:junit:4.12'
}
